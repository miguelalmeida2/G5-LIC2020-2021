Name     KeyBuffer ; 
PartNo   00 ; 
Date     14/10/20 ;
Revision 01 ;
Designer G5 ;
Company  ISEL ;
Assembly None ; 
Location  ;
Device   v750c ;

/* Start Here */
PIN 1 = CLK;
PIN 2 = ACK;
/*PIN [3..6] = [I0..3]; /*Inputs do PENC */
PIN 3 = I0;
PIN 4 = I2;
PIN 5 = I1;
PIN 6 = I3;


/*PIN [14..16] = [DEC0..2] ;*/
PIN 14 = DEC0;
PIN 15 = DEC2;
PIN 16 = DEC1;
PIN 18 = A0;
PIN 19 = Kscan;
PIN [20..23] = [Q0..Q3];
PIN 17 = Dval;

PINNODE 28 = K2;
PINNODE 27 = K3;
PINNODE 26 = A1;
PINNODE 33 = K1;
PINNODE 30 = B1;
PINNODE 31 = B0;
PINNODE 32 = K0;

/* ***** COUNTER ***** */
[K0..1].CK = !CLK;
[K0..1].sp = 'b'0;

K0.d = (K0 & !Kscan) # (!K0 & !K1 & Kscan);
K1.d = (K1 & !Kscan) # (K0 & Kscan);

/* ***** DECODER ***** */
DEC0 = K1 # K0;
DEC1 = K1 # !K0;
DEC2 = !K1 # K0;

/* ****** PENC & REGISTER ****** */
[K2..3].CK = !Kscan;
[K2..3].sp = 'b'0;

K2.d = (!I1 & !I0);
K3.d = ((!I1 & !I0) # (!I3 & !I2 & !I0));
Kpress = !I0 # !I1 # !I2 # !I3;

/* ****** Key Control ****** */
[A0..A1].ck = CLK;
[A0..A1].sp = 'b'0;

sequence[A1, A0] {
    present 0
      out Kscan;
      if Kpress next 1;
      default next 0;

    present 1
      out Kval;
      if !Kpress next 2;
      default next 1;

    present 2
      if DAC next 3;
      default next 2;

    present 3
      if !DAC next 0;
      default next 3;
}

/* Key Buffer Control */
[B0..B1].CK = !CLK;
[B0..B1].sp = 'b'0;

sequence[B1, B0] {
    present 0
    if !(Kval & !ACK) next 0;
    default next 1;

    present 1
    out DAC, Wreg;
    if !Kval next 2;
    default next 1;
 
    present 2
    out Dval, DAC;
    if ACK next 0;
    default next 2;
}


/* Output Register */
[Q0..3].CK = Wreg;
[Q0..3].sp = 'b'0;

[Q0..3].d = [K0..3];